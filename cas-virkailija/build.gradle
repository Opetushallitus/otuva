buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/libs-milestone" }
        maven { url "https://repo.spring.io/libs-snapshot" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "de.undercouch:gradle-download-task:${project.gradleDownloadTaskVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
        classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${project.jibVersion}"
        classpath "io.freefair.gradle:maven-plugin:${project.gradleMavenPluginVersion}"
        classpath "io.freefair.gradle:jsass-plugin:3.0.0"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" }
    maven { url "https://repo.spring.io/milestone/" }
    maven { url "https://repo.spring.io/snapshot/" }
    maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
    maven {
        url "https://artifactory.opintopolku.fi/artifactory/oph-sade-snapshot-local"
        mavenContent {
            snapshotsOnly()
        }
    }
    maven {
        url "https://artifactory.opintopolku.fi/artifactory/oph-sade-release-local"
        mavenContent {
            releasesOnly()
        }
    }
}

def casServerVersion = project.'cas.version'
def casWebApplicationBinaryName = "cas.war"

project.ext."casServerVersion" = casServerVersion
project.ext."casWebApplicationBinaryName" = casWebApplicationBinaryName

apply plugin: "io.freefair.war-overlay"
apply from: rootProject.file("gradle/tasks.gradle")
apply from: rootProject.file("gradle/buildversion.gradle")

apply plugin: "war"
apply plugin: "org.springframework.boot"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "com.google.cloud.tools.jib"
apply plugin: "io.freefair.jsass-java"

apply from: rootProject.file("gradle/springboot.gradle")
apply from: rootProject.file("gradle/dockerjib.gradle")

dependencies {
    compile "org.apereo.cas:cas-server-webapp${project.appServer}:${casServerVersion}"
    // Other CAS dependencies/modules may be listed here...
    compile "org.apereo.cas:cas-server-support-rest:${casServerVersion}"
    compile "org.apereo.cas:cas-server-support-session-jdbc:${casServerVersion}"
    compile "org.apereo.cas:cas-server-support-throttle-core:${casServerVersion}"
    compile "org.apereo.cas:cas-server-support-interrupt-api:${casServerVersion}"
    compile "org.apereo.cas:cas-server-support-interrupt-core:${casServerVersion}"
    compile "org.apereo.cas:cas-server-support-interrupt-webflow:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-authentication-api:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-authentication-attributes:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-authentication-throttle:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-webflow-api:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-web-api:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-tickets-api:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-services-api:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-services-authentication:${casServerVersion}"
    compile "org.apereo.cas:cas-server-core-util-api:${casServerVersion}"
    compile "org.apereo.cas:cas-server-webapp-config:${casServerVersion}"
    compile "org.apereo.cas:cas-server-webapp-init:${casServerVersion}"
    compile "com.github.kagkarlsson:db-scheduler:5.2"
    compile "org.flywaydb:flyway-core:5.2.4"
    runtime "org.postgresql:postgresql:42.2.5"
    compile "org.springframework.security:spring-security-config:5.1.2.RELEASE"
    compile "fi.vm.sade.java-utils:java-http:0.5.0-SNAPSHOT"
    compile "com.google.code.gson:gson:2.8.5"
    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    compileOnly "org.projectlombok:lombok:1.18.4"
    compileOnly "org.webjars:bootstrap:4.1.3"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.24.0"
    testCompile "org.assertj:assertj-core:3.9.1"
    testCompile "org.springframework:spring-test:5.1.3.RELEASE"
}

tasks.findByName("jibDockerBuild")
    .dependsOn(copyWebAppIntoJib, copyConfigIntoJib)
    .finalizedBy(deleteWebAppFromJib)

tasks.findByName("jib")
    .dependsOn(copyWebAppIntoJib, copyConfigIntoJib)
    .finalizedBy(deleteWebAppFromJib)

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, "seconds"
        cacheDynamicVersionsFor 0, "seconds"

        preferProjectModules()

        def failIfConflict = project.hasProperty("failOnVersionConflict") && Boolean.valueOf(project.getProperty("failOnVersionConflict"))
        if (failIfConflict) {
            failOnVersionConflict()
        }
    }
}

compileSass {
    sourceMapEnabled = true
    outputStyle = COMPRESSED
}

eclipse {
    classpath {
       downloadSources = true
       downloadJavadoc = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

war {
    entryCompression = ZipEntryCompression.STORED
}

springBoot {
    mainClassName = "org.apereo.cas.web.CasWebApplication"
}

bootWar {
    entryCompression = ZipEntryCompression.STORED
    overlays {
        // https://docs.freefair.io/gradle-plugins/current/reference/#_io_freefair_war_overlay
        // Note: The "excludes" property is only for files in the war dependency.
        // If a jar is excluded from the war, it could be brought back into the final war as a dependency
        // of non-war dependencies. Those should be excluded via normal gradle dependency exclusions.
        cas {
            from "org.apereo.cas:cas-server-webapp${project.appServer}:${casServerVersion}@war"
            provided = false
            //excludes = ["WEB-INF/lib/somejar-1.0*"]
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = "${project.gradleVersion}"
}

jib {
    from {
        image = project.baseDockerImage
    }
    to {
        image = "${project.group}/${project.name}"
        /**
         ecr-login: Amazon Elastic Container Registry (ECR)
         gcr: Google Container Registry (GCR)
         osxkeychain: Docker Hub
         */
        credHelper = "osxkeychain"
        /**
        auth {
            username = "*******"
            password = "*******"
        }
        tags = [casServerVersion]
        */
    }
    container {
        useCurrentTimestamp = true
        entrypoint = ['docker/entrypoint.sh']
        ports = ['80', '443', '8080', '8443']
        labels = [version:casServerVersion, name:project.name, group:project.group]
    }
    extraDirectories {
        path = file('src/main/jib')
        permissions = [
            '/docker/entrypoint.sh': '755'
        ]
    }
}